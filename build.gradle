buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = 'rip.deadcode'
version = '1.0.2'

sourceCompatibility = 1.8
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()
}

dependencies {
    compile(
            'io.ratpack:ratpack-core:1.5.4',
            'com.typesafe:config:1.3.3',
    )
    testCompile(
            'io.ratpack:ratpack-test:1.5.4',

            'com.google.code.gson:gson:2.8.4',

            'org.junit.jupiter:junit-jupiter-api:5.1.0',
            'com.google.truth:truth:0.40',
            'com.google.truth.extensions:truth-java8-extension:0.40',
            'org.mockito:mockito-core:2.18.3',

            'ch.qos.logback:logback-classic:1.2.3'
    )
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

bintray {
    user = project.hasProperty('BINTRAY_USER') ? BINTRAY_USER : ''
    key = project.hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ''
    pkg {
        repo = 'test'
        name = 'ratpack-hocon'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/minebreaker-tf/ratpack-hocon.git'
    }
    publications = ['bintrayPublication']
}

publishing {
    publications {
        bintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
